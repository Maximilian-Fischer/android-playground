apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.jenkinsBuildNumber = Integer.valueOf(System.env.BUILD_NUMBER ?: 0)

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "de.awesome.osr_playground"
        minSdkVersion 15
        targetSdkVersion 25
        versionName computeVersionName()
        versionCode computeVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
	signingConfigs {
		release {
		    storeFile file("../android.jks")
		    storePassword "playgroundAndroid"
		    keyAlias "AndroidKey"
		    keyPassword "playgroundAndroid"
		}
	}

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
	    signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix " (build ${jenkinsBuildNumber})"
        }
    }
}

// Returns name based on version values, e.g. '1.4.0'
def computeVersionName() {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}
// Returns auto-incrementing value, e.g 140017 for Jekins build #17
def computeVersionCode() {
    return (versionMajor * 100000) + (versionMinor * 10000) + (versionPatch * 1000) + jenkinsBuildNumber
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:25.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}
